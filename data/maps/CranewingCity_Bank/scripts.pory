const MENU_WITHDRAW = 0
const MENU_DEPOSIT = 1
const MENU_EXIT = 2

const SPECIAL_WITHDRAW = 0
const SPECIAL_DEPOSIT = 1

const OPERATION_SUCCESS = 0
const OPERATION_NOT_ENOUGH = 1
const OPERATION_TOO_MUCH = 2

//=====================================
// Map Scripts
//=====================================
mapscripts CranewingCity_Bank_MapScripts {}

//=====================================
// Global Scripts
//=====================================
script(global) CranewingCity_Bank_Boy {
    msgbox(format("I can't wait to open a bank account when I become a Pokémon Trainer!\pMom says the only way to make money is to save money, and I plan to be rich!"), MSGBOX_NPC)
    end
}

script(global) CranewingCity_Bank_Manager {
    if(flag(FLAG_BANK_ACCOUNT_CREATED)) {
        lockall
        faceplayer
        msgbox(format("Good day to you!\pWould you like to view your Pokémon Bank balance?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES) {
            bufferbankbalance(0)
            msgbox(CranewingCity_Bank_Text_Balance)
        }
        else {
            msgbox(format("Very well then!\pThe First Regional Pokémon Bank thanks you for your continued patronage!"))
        }
        closemessage
        releaseall
    }
    else {
        msgbox(format("Hi there!\nWhat can I do for you?\pOh?\nYou don't have an account?\pWell, let's get you started with a brand new Pokémon Bank account!\pJust fill out these forms…\pand these ones…\pAnd you're done!\nSee the clerk over there to make your first deposit!"), MSGBOX_NPC)
        setflag(FLAG_BANK_ACCOUNT_CREATED)
    }
    end
}

script(global) CranewingCity_Bank_Clerk {
    lock
    faceplayer
    msgbox(format("Hello and welcome to the First Regional Pokémon Bank!"))
    if(flag(FLAG_BANK_ACCOUNT_CREATED)) {
        goto(CranewingCity_Bank_BankClerkMenu)
    }
    else {
        msgbox(format("I'm so sorry, I'm afraid you don't seem to have an account with us."))
        msgbox(format("See the clerk to open one today!"))
    }
    waitmessage
    closemessage
    release
    end
}

script(global) CranewingCity_Bank_Duskull {
    msgbox(format("Oawwoohhoo!"), MSGBOX_NPC)
    end
}

//=====================================
// Local Scripts
//=====================================
script(local) CranewingCity_Bank_BankClerkMenu {
    msgbox("How may I help you?")
    multichoice(0, 0, MULTI_BANK, FALSE)
    switch(var(VAR_RESULT)) {
        case MENU_WITHDRAW:
            call(CranewingCity_Bank_BankClerkWithdraw)
        case MENU_DEPOSIT:
            call(CranewingCity_Bank_BankClerkDeposit)
        case MENU_EXIT:
            msgbox("Thank you for stopping by.\nWe hope to see you again!")
        case MULTI_B_PRESSED:
            msgbox("Thank you for stopping by.\nWe hope to see you again!")
    }
    waitmessage
    closemessage
    release
}

script(local) CranewingCity_Bank_BankClerkWithdraw {
    setvar(VAR_0x8004, SPECIAL_WITHDRAW)
    special(Bank)
    waitstate
    switch(var(VAR_RESULT)) {
        case OPERATION_SUCCESS:
            playse(SE_SHOP)
            waitse
            msgbox("Thank you for your business!\nPlease come back again soon!")

        case OPERATION_NOT_ENOUGH:
            msgbox("We don't have that much…")
            goto(CranewingCity_Bank_BankClerkMenu)

        case OPERATION_TOO_MUCH:
            msgbox("You can't carry that much!")
            goto(CranewingCity_Bank_BankClerkMenu)

        default:
            goto(CranewingCity_Bank_BankClerkMenu)
    }
    return
}

script(local) CranewingCity_Bank_BankClerkDeposit {
    setvar(VAR_0x8004, SPECIAL_DEPOSIT)
    special(Bank)
    waitstate
    switch(var(VAR_RESULT)) {
        case OPERATION_SUCCESS:
            playse(SE_SHOP)
            waitse
            msgbox("Your money is safe with us!")

        case OPERATION_NOT_ENOUGH:
            msgbox("You don't have that much…")
            goto(CranewingCity_Bank_BankClerkMenu)

        case OPERATION_TOO_MUCH:
            msgbox("We can't store that much!")
            goto(CranewingCity_Bank_BankClerkMenu)

        default:
            goto(CranewingCity_Bank_BankClerkMenu)
    }
    return
}

//=====================================
// Text
//=====================================
text CranewingCity_Bank_Text_Balance {
    format (
        "You current balance is {STR_VAR_1}.\pThank you for your continued patronage!"
    )
}

//=====================================
// Movement
//=====================================

//=====================================
// Raw
//=====================================

const BRADLEY = 2
const AMPURE = 3

const WARP_RESCUE = 2

// VAR_NEOBAY_STATE values
const NEOBAY_STATE_NEW_GAME = 0
const NEOBAY_STATE_MET_WILLOW = 1
const NEOBAY_STATE_RECEIVED_STARTER = 2

// VAR_AMPURE_RESCUE_STATE values
const AMPURE_RESCUE_STARTED = 1
const AMPURE_RESCUE_IN_MEADOW = 2
const AMPURE_RESCUE_FIRST_BATTLE_DONE = 3
const AMPURE_RESCUE_SECOND_BATTLE_DONE = 4
const AMPURE_RESCUE_CAPTURE_DONE = 5

// State descriptions for VAR_NEOBAY_STATE
// 1: Met Professor Willow
// 2: Received Pokemon
// 3: Saved Ampure
// 4: Saw flashback
// 5: Received running shoes
// 6: Departed for Sunset Village

// State descriptions for VAR_AMPURE_RESCUE_STATE
// 1: Started mission
// 2: Moved to meadow
// 3: Fought first battle
// 4: Fought second battle
// 5: Finished capture
// 6: Returned to Willow

//=====================================
// Map Scripts
//=====================================
mapscripts NeoBay_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: NeoBay_OnTransition
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_STARTED: NeoBay_EventScript_SetupBradleyAtBoat
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE: NeoBay_EventScript_SetupBradleyForAmpure
    ]
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_STARTED: NeoBay_EventScript_BradleyLeadsToGrass
        FLAG_WILD_ENCOUNTER, 1: NeoBay_OnCompletedWildEncounter
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE: NeoBay_EventScript_AmpureCapture
    ]
}

//=====================================
// Global Scripts
//=====================================
script NeoBay_EventScript_GoToWillowBradleyTriggerTop {
    lockall
    addobject(BRADLEY)
    applymovement(OBJ_EVENT_ID_PLAYER, mFaceLeft)
    setobjectxy(BRADLEY, 12, 18)
    applymovement(BRADLEY, mBradleyApproachPlayerTop)
    waitmovement(0)
    call(NeoBay_EventScript_BradleyIntroText)
    applymovement(BRADLEY, mBradleyEscortsPlayerTop)
    applymovement(OBJ_EVENT_ID_PLAYER, mPlayerEscortedByBradleyTop)
    waitmovement(0)
    turnobject(BRADLEY, DIR_WEST)
    call(NeoBay_EventScript_BradleyShowBoatText)
    releaseall
    end
}

script NeoBay_EventScript_GoToWillowBradleyTriggerBottom {
    lockall
    addobject(BRADLEY)
    applymovement(OBJ_EVENT_ID_PLAYER, mFaceLeft)
    setobjectxy(BRADLEY, 12, 18)
    applymovement(BRADLEY, mBradleyApproachPlayerBottom)
    waitmovement(0)
    call(NeoBay_EventScript_BradleyIntroText)
    applymovement(BRADLEY, mBradleyEscortsPlayerBottom)
    applymovement(OBJ_EVENT_ID_PLAYER, mPlayerEscortedByBradleyBottom)
    waitmovement(0)
    turnobject(BRADLEY, DIR_WEST)
    call(NeoBay_EventScript_BradleyShowBoatText)
    releaseall
    end
}

script NeoBay_EventScript_TownSign {
    msgbox(NeoBay_Text_TownSign, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_DockSign {
    msgbox(NeoBay_Text_DockSign, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_PlayerMailbox {
    msgbox(NeoBay_Text_PlayerMailbox, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_OtherMailbox {
    msgbox(NeoBay_Text_OtherMailbox, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_BradleyStopsPlayer {
    lockall
    msgbox(format("Bradley: What are you doing? We need to find Ampure before it gets away!"))
    waitmessage
    closemessage
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    switch(var(VAR_TEMP_0)) {
        case 9:
            applymovement(OBJ_EVENT_ID_PLAYER, mWalkDown)
        case 10:
            applymovement(OBJ_EVENT_ID_PLAYER, mWalkDown)
        case 16:
            applymovement(OBJ_EVENT_ID_PLAYER, mWalkLeft)
        default:
            applymovement(OBJ_EVENT_ID_PLAYER, mWalkLeft)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)
    releaseall
    end
}

script NeoBay_EventScript_BradleyConversation {
    lock
    faceplayer
    switch(var(VAR_NEOBAY_STATE)) {
        case NEOBAY_STATE_RECEIVED_STARTER:
            msgbox(format("Bradley: I haven't seen Ampure anywhere. You should check that tall grass!"))
        default:
            msgbox(format("--Text Here--"))
    }
    waitmessage
    closemessage
    release
}

//=====================================
// Local Scripts
//=====================================
script(local) NeoBay_OnTransition {
    setflag(FLAG_VISITED_NEOBAY_TOWN)
    call(Common_EventScript_SetupRivalGfxId)
    switch (var(VAR_NEOBAY_STATE)) {
        case NEOBAY_STATE_NEW_GAME:
            addobject(BRADLEY)
            end
        case NEOBAY_STATE_RECEIVED_STARTER:
            addobject(BRADLEY)
            end
        default:
            addobject(BRADLEY)
            end
    }
}

script(local) NeoBay_EventScript_BradleyIntroText {
    msgbox(NeoBay_Text_BradleyIntroStart, MSGBOX_DEFAULT)
    applymovement(BRADLEY, mBradleyFidgetsRight)
    waitmovement(0)
    msgbox(NeoBay_Text_BradleyIntroStartLetsGo, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    return
}

script(local) NeoBay_EventScript_BradleyShowBoatText {
    msgbox(NeoBay_Text_BradleyShowBoatStart, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(BRADLEY, mEnterShip)
    applymovement(OBJ_EVENT_ID_PLAYER, mEnterShip)
    waitmovement(0)
    warp(MAP_NEO_BAY_PROFESSOR_WILLOWS_LAB, 0, 0, 0)
    return
}

script(local) NeoBay_EventScript_BradleyLeadsToGrass {
    lockall
    applymovement(BRADLEY, mBradleyWalksUpAndLooksAtPlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, mPlayerWalksUpAndLooksAtBradley)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Bradley: I think I see some tracks here. They're heading towards… the meadow? I wonder why he'd go there…"))
    msgbox(format("{PLAYER}, I think there may be wild Pokémon where we're going, so be ready!"), MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(BRADLEY, mBradleyEscortsPlayerToMeadow)
    applymovement(OBJ_EVENT_ID_PLAYER, mPlayerEscortsBradleyToMeadow)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(BRADLEY, mBradleyLooksAroundAndFidgets)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: Help me look for Ampure, {PLAYER}! He's probably hiding in this tall grass. If you walk around enough, he should appear!"), MSGBOX_AUTOCLOSE)
    waitmessage
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_IN_MEADOW)
    copyobjectxytoperm(BRADLEY)
    // clearflag(FLAG_HIDE_NEO_BAY_BRADLEY)
    releaseall
    end
}

script(local) NeoBay_EventScript_SetupBradleyAtBoat {
    addobject(BRADLEY)
    addobject(AMPURE)
    setobjectxy(BRADLEY, 28, 33)
    turnobject(BRADLEY, DIR_NORTH)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    return
}

script(local) NeoBay_OnCompletedWildEncounter {
    clearflag(FLAG_WILD_ENCOUNTER)
    switch(var(VAR_AMPURE_RESCUE_STATE)) {
        case AMPURE_RESCUE_IN_MEADOW:
            call(NeoBay_EventScript_AmpureRescueAfterFirst)
        case AMPURE_RESCUE_FIRST_BATTLE_DONE:
            call(NeoBay_EventScript_AmpureRescueAfterSecond)
    }
    return
}

script(local) NeoBay_UpdateBradleyPositionAfterEncounter {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if(var(VAR_TEMP_1) > 27) {
        setobjectxyperm(BRADLEY, 13, 27)
        turnobject(BRADLEY, DIR_SOUTH)
        applymovement(BRADLEY, mBradleyFidgetsDown)
    }
    elif(var(VAR_TEMP_1) > 19) {
        setobjectxyperm(BRADLEY, 13, 22)
        turnobject(BRADLEY, DIR_WEST)
        applymovement(BRADLEY, mBradleyFidgetsLeft)
    }
    else {
        setobjectxyperm(BRADLEY, 12, 17)
        turnobject(BRADLEY, DIR_WEST)
        applymovement(BRADLEY, mBradleyFidgetsLeft)
    }
    waitmovement(BRADLEY)
    return
}

script(local) NeoBay_EventScript_AmpureRescueAfterFirst {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_FIRST_BATTLE_DONE)
    call(NeoBay_UpdateBradleyPositionAfterEncounter)
    msgbox(format("Bradley: Did you find him?\l… … oh, well keep looking.\lHere, I'll heal your Pokémon for you."))
    waitmessage
    closemessage
    call(Common_EventScript_OutOfCenterPartyHeal)
    releaseall
    return
}

script(local) NeoBay_EventScript_AmpureRescueAfterSecond {
    lockall
    call(NeoBay_UpdateBradleyPositionAfterEncounter)
    msgbox(format("Bradley: Any luck this time?\l… … oh, well keep looking.\lHere, I'll heal your Pokémon again."))
	playfanfare(MUS_HEAL)
	waitfanfare
    closemessage
	special(HealPlayerParty)
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE)
    warp(MAP_NEO_BAY, 0xFF, 12, 23)
    releaseall
    return
}

script(local) NeoBay_EventScript_SetupBradleyForAmpure {
    setobjectxyperm(BRADLEY, 11, 23)
    turnobject(BRADLEY, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
}

script(local) NeoBay_EventScript_AmpureCapture {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_CAPTURE_DONE)
	fadescreen(FADE_FROM_BLACK)
    applymovement(AMPURE, mAmpureRunsUpToPlayer)
    waitmovement(AMPURE)
    applymovement(AMPURE, mExclamationPoint)
    applymovement(BRADLEY, mLookUpStartled)
    applymovement(OBJ_EVENT_ID_PLAYER, mLookUpStartled)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Bradley: I think he likes you, {PLAYER}! Just stay still, I'll catch him!"))
    waitmessage
	special(SavePlayerParty)
	special(PutRivalMonInPlayerParty)
    special(StartBradleyAmpureCatch)
    waitstate
    msgbox(format("Bradley: ALRIGHT! And that was my first time, too!\lLet's go back and return Ampure to Prof. Willow!"))
    warp(MAP_NEO_BAY_PROFESSOR_WILLOWS_LAB, 0xFF, 8, 6)
    releaseall
    return
}

//=====================================
// Text
//=====================================
text NeoBay_Text_TownSign {
    format(
        "NEO BAY\n"
        "“The most isolated town in Caldera. Where new things begin.”"
    )
}

text NeoBay_Text_DockSign {
    format(
        "NEO BAY DOCK\n"
        "“The only access point for regional visitors, rare as they are.”"
    )
}

text NeoBay_Text_PlayerMailbox {
    format(
        "{PLAYER}'s Mailbox:\n"
        "Looks like there's no mail today…"
    )
}

text NeoBay_Text_OtherMailbox {
    format(
        "I shouldn't go snooping through other people's mail!"
    )
}

text NeoBay_Text_BradleyIntroStart {
    format(
        "Bradley: Well, if it isn't {PLAYER}!\n"
        "So you've finally woken up, sleepyhead? Honestly, you've got a whole lot of nerve, making a friend wait like that!\l"
        "… … Nah, I'm just kidding! I'm not that bad of a guy, y'know? Anyway!"
    )
}

text NeoBay_Text_BradleyIntroStartLetsGo {
    format(
        "Bradley: I called you here because I've got a once-in-a-lifetime opportunity for ya!\l"
        "There's someone you absolutely, definitely, positively HAVE to meet! Trust me, it'll be worth your time. I promise!\l"
        "Come on, come on! This way!"
    )
}

text NeoBay_Text_BradleyShowBoatStart {
    format(
        "Bradley: Look! You've noticed this sleek-looking boat moored here for a while now, right? "
        "Well, this boat belongs to none other than Prof. Willow!\l"
        "… … …\l"
        "… … …\l"
        "What, you haven't heard of her? I guess you're not quite as worldly as your pal Bradley!\l"
        "… Anyway, she's a world-famous Pokémon Professor, known for her research on ancient Pokémon fossils!\l"
        "She's cool, calm, composed … and when it comes to Pokémon, she's a genius! I really admire her!\l"
        "Come on, let's go meet her now!"
    )
}

//=====================================
// Movement
//=====================================
movement mBradleyApproachPlayerTop {
    walk_down * 5
    walk_right
    walk_down
    walk_right * 4
}

movement mBradleyApproachPlayerBottom {
    walk_down * 6
    walk_right
    walk_down
    walk_right * 4
}

movement mBradleyFidgetsRight {
    walk_in_place_fast_right * 4
}

movement mBradleyFidgetsLeft {
    walk_in_place_fast_left * 4
}

movement mBradleyFidgetsDown {
    walk_in_place_fast_down * 4
}

movement mBradleyFidgetsUp {
    walk_in_place_fast_up * 4
}

movement mBradleyEscortsPlayerTop {
    walk_down
    walk_right * 8
    walk_down * 6
    walk_right * 4
}

movement mBradleyEscortsPlayerBottom {
    walk_up
    walk_right * 8
    walk_down * 7
    walk_right * 4
}

movement mPlayerEscortedByBradleyTop {
    walk_left
    walk_down
    walk_right * 8
    walk_down * 6
    walk_right * 3
}

movement mPlayerEscortedByBradleyBottom {
    walk_left
    walk_up
    walk_right * 8
    walk_down * 7
    walk_right * 3
}

movement mEnterShip {
    walk_down * 2
    set_invisible
}

movement mBradleyWalksUpAndLooksAtPlayer {
    walk_up
    face_right
}

movement mPlayerWalksUpAndLooksAtBradley {
    walk_up
    face_left
}

movement mBradleyEscortsPlayerToMeadow {
    walk_up * 3
    walk_left * 4
    walk_up * 4
    walk_left * 12
    walk_up
}

movement mPlayerEscortsBradleyToMeadow {
    walk_up * 3
    walk_left * 5
    walk_up * 4
    walk_left * 12
    face_up
}

movement mBradleyLooksAroundAndFidgets {
    face_right
    delay_16 * 4
    walk_left
    walk_up * 2
    face_left
    delay_16 * 3
    face_up
    delay_16 * 3
    face_right
    delay_16 * 3
    face_up
    delay_16 * 3
    walk_in_place_fast_down * 4
    walk_left
    delay_16 * 3
    face_up
    delay_16 * 3
    face_left
    delay_16 * 3
    walk_right
    walk_down
}

movement mWalkLeft {
    walk_left
}

movement mAmpureRunsUpToPlayer {
    walk_fast_down * 9
}

movement mLookUpStartled {
    emote_exclamation_mark
    face_up
}

//=====================================
// Raw
//=====================================
const PLAYER = OBJ_EVENT_ID_PLAYER
const NURSE = 1
const BRADLEY = 4

// VAR_CRANEWING_CITY_STATE values
const CRANEWING_STATE_INIT = 0
const CRANEWING_STATE_SAW_GLASSWORKS = 1
const CRANEWING_STATE_MET_BALFOUR = 2
const CRANEWING_STATE_SAW_BRADLEY_AT_CENTER = 3

//=====================================
// Map Scripts
//=====================================
mapscripts CranewingCity_PokemonCenter_MapScripts  {
    MAP_SCRIPT_ON_TRANSITION: CranewingCity_PokemonCenter_OnTransition
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_CRANEWING_CITY_STATE, CRANEWING_STATE_MET_BALFOUR: CranewingCity_PokemonCenter_OnWarp
    ]
}

//=====================================
// Global Scripts
//=====================================
script(global) CranewingCity_PokemonCenter_Nurse {
    setvar(VAR_0x800B, NURSE)
    call(Common_EventScript_PkmnCenterNurse)
    waitmessage
    waitbuttonpress
    release
    end
}

script(global) CranewingCity_PokemonCenter_FatMan {
    msgbox(CranewingCity_Text_FatMan, MSGBOX_NPC)
    end
}

script(global) CranewingCity_PokemonCenter_Beauty {
    msgbox(CranewingCity_Text_Beauty, MSGBOX_NPC)
    end
}

//=====================================
// Local Scripts
//=====================================
script(local) CranewingCity_PokemonCenter_OnTransition {
	setrespawn(HEAL_LOCATION_CRANEWING_CITY)
    end
}

script(local) CranewingCity_PokemonCenter_OnWarp {
    lock
    turnobject(PLAYER, DIR_EAST)
    turnobject(BRADLEY, DIR_WEST)
    delay(30)
    msgbox(CranewingCity_PokemonCenter_Text_Bradley)
    closemessage
    applymovement(BRADLEY, mBradleyLeavesTheCenter)
    waitmovement(0)
    removeobject(BRADLEY)
    setflag(FLAG_HIDE_BRADLEY_CRANEWING_CENTER) // redundant for clarity
    setvar(VAR_CRANEWING_CITY_STATE, CRANEWING_STATE_SAW_BRADLEY_AT_CENTER)
    release
    end
}

//=====================================
// Text
//=====================================
text CranewingCity_Text_FatMan {
    format(
        "Hiding?\nNo, not at all!\pI was just studying "
        "this here plant.\pYou've probably seen plants just "
        "like this one in other Pokémon centers, right?\p"
        "Well, they were all grown in old Pinedale Forest "
        "before it burned down.\pThe ones you see today are "
        "all just grafts of an old tree before the fire!\p"
        "… … …\pOkay, fine, I made it up.\pI'm hiding from "
        "those scary people in black uniforms!"
    )
}

text CranewingCity_Text_Beauty {
    format(
        "Oh my goodness, you will not BELIEVE the things "
        "they've got on these shelves!\pThey've got my "
        "favorite romance series!\pAll but the last one…"
    )
}

text CranewingCity_PokemonCenter_Text_Bradley {
    format (
        "Bradley: They fixed me right up!\p"
        "It didn't take long at all!\n"
        "Now we have to beat Froy and--\p"
        "What?\p"
        "You've already beaten him!?\p"
        "… I guess I have some catching up to do.\p"
        "Prof. Willow is waiting for us back in "
        "Softwind City.\pYou go and meet her, I'll "
        "stay here and get ready to challenge the gym."
    )
}

//=====================================
// Movement
//=====================================
movement mBradleyLeavesTheCenter {
    walk_down
    walk_left * 3
    walk_down * 2
    set_invisible
}

//=====================================
// Raw
//=====================================
